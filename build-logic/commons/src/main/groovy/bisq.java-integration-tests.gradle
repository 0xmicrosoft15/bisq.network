sourceSets {
    integrationTest {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}

configurations {
    integrationTestImplementation.extendsFrom implementation
    integrationTestRuntimeOnly.extendsFrom runtimeOnly
}

dependencies {
    integrationTestAnnotationProcessor libs.lombok
    integrationTestCompileOnly libs.lombok

    integrationTestImplementation libs.slf4j.api
    integrationTestImplementation libs.logback.core
    integrationTestImplementation libs.logback.classic

    integrationTestImplementation libs.junit.jupiter
    integrationTestImplementation libs.assertj.core
}

tasks.register('integrationTest', Test) {
    description = 'Runs integration tests.'
    group = 'verification'

    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    shouldRunAfter test

    useJUnitPlatform()

    testLogging {
        events "passed"
    }
}