plugins {
    id 'bisq.gradle.plugin.BisqPlugin'
}

sourceSets {
    integrationTest {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}

configurations {
    integrationTestImplementation.extendsFrom implementation
    integrationTestRuntimeOnly.extendsFrom runtimeOnly
}

dependencies {
    implementation project(':socks5-socket-channel')

    implementation libs.google.guava
    implementation libs.failsafe
    implementation libs.apache.commons.compress
    implementation libs.tukaani

    implementation libs.chimp.jsocks
    implementation libs.chimp.jtorctl

    implementation libs.bundles.bisq.tor.binaries

    integrationTestImplementation libs.junit.jupiter
    integrationTestImplementation libs.assertj.core
}

tasks.register('integrationTest', Test) {
    description = 'Runs integration tests.'
    group = 'verification'

    testClassesDirs = sourceSets.intTest.output.classesDirs
    classpath = sourceSets.intTest.runtimeClasspath
    shouldRunAfter test

    useJUnitPlatform()

    testLogging {
        events "passed"
    }
}